plugins { id 'org.beryx.runtime' version '1.8.4' }

sourceCompatibility = 1.7
sourceSets.main.java.srcDirs = [ "src/" ]
sourceSets.main.resources.srcDirs = ["../android/assets"]

def osName = System.getProperty('os.name').toLowerCase(Locale.ROOT)

mainClassName = "com.phys.template.desktop.DesktopLauncher"
project.ext.assetsDir = new File("../android/assets")


task runMyGame(dependsOn: classes, type: JavaExec) {
    main = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.assetsDir
    ignoreExitValue = true
}

task debug(dependsOn: classes, type: JavaExec) {
    main = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.assetsDir
    ignoreExitValue = true
    debug = true
}

task dist(type: Jar) {
    destinationDirectory = file("$buildDir/lib")
    manifest {
        attributes 'Main-Class': project.mainClassName
    }
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

jpackageImage.dependsOn dist

// Store the parameters you want to pass the texturePacker here...
project.ext.texturePacker = [ "core/rawAssets", "android/assets/skin", "uiskin" ]

// Import the texture packer
import com.badlogic.gdx.tools.texturepacker.TexturePacker

// Add a new task that packs the textures for you
task packTextures {
    doLast {
        if (project.ext.has('texturePacker')) {
            logger.info "Calling TexturePacker: "+texturePacker
            TexturePacker.Settings settings = new TexturePacker.Settings();
            settings.flattenPaths = true;
            TexturePacker.process(settings, texturePacker[0], texturePacker[1], texturePacker[2])
        }
    }
}

dist.dependsOn classes

eclipse.project.name = appName + "-desktop"

runtime {
    options = ['--strip-debug',
               '--compress', '2',
               '--no-header-files',
               '--no-man-pages',
               '--strip-native-commands',
               '--vm', 'server']
    modules = ['java.base' ,
               'java.desktop',
               'jdk.unsupported']
    distDir = file(buildDir)

    jpackage {
        jpackageHome = 'C:\\Users\\vuvka\\.jdks\\temurin-18.0.1'
        mainJar = dist.archiveFileName.get()
        if (osName.contains('windows')) {
            imageOptions = ["--icon", file("../icons/icon.ico")]
        } else if (osName.contains('linux')) {
            imageOptions = ["--icon", file("../icons/icon.png")]
        } else if (osName.contains('mac')) {
            imageOptions = ["--icon", file("../icons/icon.icns")]
        }
    }
}

